within example_model;
model O_ISS

  // 输入端口：来自CPS的输入（5维）
  Modelica.Blocks.Interfaces.RealInput from_CPS[5] "来自CPS的输入" annotation(
    Placement(transformation(origin = {-120, 40}, extent = {{-10, -10}, {10, 10}}),
              iconTransformation(origin = {-114, 0}, extent = {{10, -10}, {-10, 10}}, rotation = 180)));

  // 输入端口：来自WDS的输入（5维）
  Modelica.Blocks.Interfaces.RealInput from_WDS[5] "来自WDS的输入" annotation(
    Placement(transformation(origin = {-120, 60}, extent = {{-10, -10}, {10, 10}}),
              iconTransformation(origin = {114, 0}, extent = {{-10, -10}, {10, 10}}, rotation = -180)));

  // 输入端口：来自TES的输入（5维）
  Modelica.Blocks.Interfaces.RealInput from_TES[5] "来自TES的输入" annotation(
    Placement(transformation(origin = {-120, 20}, extent = {{-10, -10}, {10, 10}}),
              iconTransformation(origin = {0, 114}, extent = {{-10, -10}, {10, 10}}, rotation = 270)));

  // 输出端口：输出到SDS系统（5维）
  Modelica.Blocks.Interfaces.RealOutput to_SDS[5] "输出到SDS系统" annotation(
    Placement(transformation(origin = {110, -20}, extent = {{-10, -10}, {10, 10}}),
              iconTransformation(origin = {0, -114}, extent = {{-10, -10}, {10, 10}}, rotation = -90)));

  // 状态变量：系统中5种物质的储存量
  Real I[5](start = {0, 0, 0, 0, 0}) "系统中5种物质的储存量";
  Real outflow[5] "总输出流";

  // 参数定义
  parameter Real T = 12 "平均滞留时间 (mean residence time)";
  parameter Real decay_loss[5] (each unit="1/h") = {6.4e-6, 0, 0, 0, 0} "Tritium decay loss for 5 materials (放射性衰变损失)";
  parameter Real nonradio_loss[5] (each unit="1") = {0.0001, 0.0001, 0, 0, 0} "非放射性损失";
  parameter Real threshold = 20 "铺底量";
  
  // 辅助变量：计算I的总和
  Real I_total "I的5个分量之和";

    Real decay_rate[5] "衰变速率";
    Real leak_rate[5] "泄漏速率";
equation
  // 计算I的总和
  I_total = sum(I);
  
  // 计算每种物质的动态变化和输出
  for i in 1:5 loop
    // 根据储存量是否超过阈值，分为两种情况
    if I_total > threshold then
      der(I[i]) = from_CPS[i] + from_TES[i] + from_WDS[i] - (1 + nonradio_loss[i]) * (1 - threshold / I_total) * I[i] / T  - decay_loss[i] * I[i];
      outflow[i] = (1 - threshold / I_total) * I[i] / T;
        leak_rate[i] = nonradio_loss[i] * (1 - threshold / I_total) * I[i] / T;
    else
      der(I[i]) = from_CPS[i] + from_TES[i] + from_WDS[i] - nonradio_loss[i] * I[i]/T  - decay_loss[i] * I[i];
      outflow[i] = 0;
        leak_rate[i] = nonradio_loss[i] * I[i]/T;
    end if;
    // 输出流分配到SDS
    to_SDS[i] = outflow[i];
      decay_rate[i] = decay_loss[i]*I[i];
    end for;

annotation(
    Icon(graphics = {
      Line(origin = {-100, -100}, points = {{0, 0}, {200, 0}}, color = {0, 0, 127}),
      Line(origin = {-100, 100}, points = {{0, 0}, {200, 0}}, color = {0, 0, 127}),
      Line(origin = {-100, -100}, points = {{0, 0}, {0, 200}}, color = {0, 0, 127}),
      Line(origin = {100, -100}, points = {{0, 0}, {0, 200}}, color = {0, 0, 127}),
      Rectangle(fillColor = {255, 85, 0}, fillPattern = FillPattern.Solid, extent = {{-100, 100}, {100, -100}}),
      Text(origin = {5, 7}, extent = {{-75, 33}, {75, -33}}, textString = "O-ISS", fontName = "Arial")}
    ),
    uses(Modelica(version = "4.0.0"))
);

end O_ISS;
